prototype(Flowpack.SearchPlugin:Search) {
    # todo can be remove when https://github.com/skurfuerst/Flowpack.SearchPlugin/pull/14 is merged
    @cache {
        context {
            3 = 'documentNode'
        }
    }
}

prototype(Neos.MarketPlace:Search) >
prototype(Neos.MarketPlace:Search) < prototype(Flowpack.SearchPlugin:Search) {
    attributes {
        class = 'pm-block pm-search'
    }

    actionNode = ${node}
    @context.actionNode = ${this.actionNode}

    hasSearchQuery = ${String.isBlank(request.arguments.search) ? false : true}

    query = ${MarketPlaceSearch.query(documentNode)}
    query.@process.nodeType = ${this.hasSearchQuery ? value.nodeType('Neos.MarketPlace:Document') : value.nodeType('Neos.MarketPlace:Package')}
    query.@process.fulltext = ${this.hasSearchQuery ? value.fulltext(this.searchTerm) : value}
    query.@process.sort = ${this.hasSearchQuery ? value : value.sortDesc('__lastVersion.time')}
    query.@process.log = ${value.log('marketplace')}

    searchQuery = ${this.query}

    prototype(Flowpack.SearchPlugin:Search.Form) {
        templatePath = 'resource://Neos.MarketPlace/Private/Templates/NodeTypes/Search.Form.html'

        actionUri = NodeUri {
            node = ${actionNode}
        }
    }

    searchResultRenderer {
        attributes {
            class = 'pm-search-results'
        }
    }
}
