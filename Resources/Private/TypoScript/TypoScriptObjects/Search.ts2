prototype(Flowpack.SearchPlugin:Search) {
    templatePath = 'resource://Neos.MarketPlace/Private/Templates/NodeTypes/Search.html'

    configuration {
        itemsPerPage = 25
        insertAbove = ${false}
        insertBelow = ${true}
        maximumNumberOfLinks = 6
    }

    # todo can be remove when https://github.com/skurfuerst/Flowpack.SearchPlugin/pull/14 is merged
    @cache {
        context {
            3 = 'documentNode'
        }
    }
}

prototype(Neos.MarketPlace:Search) >
prototype(Neos.MarketPlace:Search) < prototype(Flowpack.SearchPlugin:Search) {
    attributes {
        class = 'market-block'
    }

    actionNode = ${node}
    @context.actionNode = ${this.actionNode}

    hasSearchQuery = ${String.isBlank(request.arguments.search) ? false : true}

    query = ${MarketPlaceSearch.query(documentNode)}
    query.@process.nodeType = ${this.hasSearchQuery ? value.nodeType('Neos.MarketPlace:Document') : value.nodeType('Neos.MarketPlace:Package')}
    query.@process.fulltext = ${this.hasSearchQuery ? value.fulltext(this.searchTerm) : value}
    query.@process.sort = ${this.hasSearchQuery ? value : value.sortDesc('__lastActivity')}
    query.@process.log = ${value.log('marketplace')}

    searchQuery = ${this.query}

    prototype(Flowpack.SearchPlugin:Search.Form) {
        templatePath = 'resource://Neos.MarketPlace/Private/Templates/NodeTypes/Search.Form.html'

        actionUri = NodeUri {
            node = ${actionNode}
        }
    }

    searchResultRenderer {
        attributes {
            class = 'search-results'
        }
    }
}
