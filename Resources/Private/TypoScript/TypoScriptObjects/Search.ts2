prototype(Flowpack.SearchPlugin:Search) {
    # todo can be remove when https://github.com/skurfuerst/Flowpack.SearchPlugin/pull/14 is merged
    @cache {
        context {
            3 = 'documentNode'
        }
    }
}

prototype(Neos.MarketPlace:Search) >
prototype(Neos.MarketPlace:Search) < prototype(Flowpack.SearchPlugin:Search) {
    attributes {
        class = 'pm-block pm-search'
    }

    actionNode = ${node}
    @context.actionNode = ${this.actionNode}

    hasSearchQuery = ${request.arguments.search}

    query = ${MarketPlaceSearch.query(site)}
    query.@process.fulltext = ${value.fulltext(this.searchTerm)}
    query.@process.log = ${value.log('marketplace')}

    searchQuery = ${this.hasSearchQuery ? this.query : null}

    prototype(Flowpack.SearchPlugin:Search.Form) {
        templatePath = 'resource://Neos.MarketPlace/Private/Templates/NodeTypes/Search.Form.html'

        actionUri = NodeUri {
            node = ${actionNode}
        }
    }

    searchResultRenderer {
        attributes {
            class = 'pm-search-results'
        }
    }
}
